
# Fertilization Ordinance

!!! abstract
    The German Fertilization Ordinance implements the EU sNitrates Directive
    in Germany as well as further environmental regulations. It consists of numerous measures
    which prescribe how farmers are allowed to use nutrients from manure and chemical
    fertilizer as well as further management specifications. The most prominent measures of the
    Fertilization Ordinance are included in Farmdyn, being (1) nuntrient balance restrictions,
    (2) an organic nitrogen application threshold, (3) required manure storage capacities,
    (4) banning periods for fertilizer application, (5) restrictions of fertilizer application in autumn,
    (6) a binding fertilizer planning (7) compulsory low-emission manure application techniques.

 The equations regarding the Fertilization Ordinace are mainly found in the Fertilization Ordinance
 module (duev_module.gms). Measures with regard to the storage capacity are partly found in the
 manure module. Farmdyn is used to asses the revision of the Fertilization Ordinance in 2017. Therefore,
 the Ordinance from 2007 and 2017 can be directly selected in the GUI to active the corresponding measues. In addition,
 thresholds and requirements can seperatly be modified in the GUI.

## Nutrient balance restrictions

  The German Fertilization Ordinance requires that farms calculate a nutrient
  balance on an annual basis for nitrogen and phosphate (DüV 2007;DüV 2007).
  This balance combines nutrient inputs via manure and synthetic fertilizer with nutrient
  removal via the harvested crops. The surplus, i.e. the balance, is not allowed to exceed a certain
  threshold.

Nutrient removal via harvested product is calculated, depending on the yield level and nutrient
content of the harvested product. It covers the main harvested product as well as straw from
cereal production which can be sold in Farmdyn.

[embedmd]:# (N:/em/work1/FarmDyn/FarmDyn_QM/gams/model/duev_module.gms GAMS /nutRemovalDuev_\(.*?\.\./ /;/)
```GAMS
nutRemovalDuev_(nut,tCur(t),nCur) $ t_n(t,nCur) ..

       v_nutRemovalDuev(nut,t,nCur)
            =e=

                sum( (c_s_t_i(crops,plot,till,intens)), v_cropHa(crops,plot,till,intens,t,nCur)
                            * sum( (plot_soil(plot,soil),curProds), p_OCoeffC(crops,soil,till,intens,curProds,t)
                                     * p_nutContent(crops,curProds,nut)*10 )   )


               +   sum( (c_s_t_i(crops,plot,till,intens)) $ cropsResidueRemo(crops),  v_residuesRemoval(crops,plot,till,intens,t,nCur)
                          *sum( (plot_soil(plot,soil),curProds),  p_OCoeffResidues(crops,soil,till,intens,curProds,t)
                                     *  p_nutContent(crops,curProds,nut) * 10  )  )

                                                                      ;
```

Nutrient input via synthetic fertilizer is calculated.

[embedmd]:# (N:/em/work1/FarmDyn/FarmDyn_QM/gams/model/duev_module.gms GAMS /synthAppliedDueV_\(.*?\.\./ /;/)
```GAMS
synthAppliedDueV_(nut,tCur(t),nCur)  $ t_n(t,nCur)..

           v_synthAppliedDueV(nut,t,nCur)    =e=

                              sum( (c_s_t_i(crops,plot,till,intens),syntFertilizer,m),
                                 v_syntDist(crops,plot,till,intens,syntFertilizer,t,nCur,m)
                                           * p_nutInSynt(syntFertilizer,nut) )      ;
```
Input via animal manure is calculated.

@Rienne: add equation model\duev_module.gms - nutExcrDueV_

Input via digestates from biogas production is calculated (only digestate from plant origin as for instance
silag maize).

@Rienne: add equation model\duev_module.gms - nutBiogasDuev_

In the equation *nutBalDuev\_*, nutrient input and output are combined. Manure N is accounted with
factors defined by the Fertilization Ordinance. As additional nutrient input
and outputs, the import and export of manure nutrients in included into the equation.

[embedmd]:# (N:/em/work1/FarmDyn/FarmDyn_QM/gams/model/duev_module.gms GAMS /nutBalDueV_\(.*?\.\./ /;/)
```GAMS
nutBalDueV_(nut,tCur(t),nCur) $ t_n(t,nCur) ..

$iftheni.h %herd% == true

*   --- Nutrients excreted from animals time specific loss factor

           v_nutExcrDuev(nut,t,nCur)  *  p_nutEffectivDueVNv(nut)
$endif.h

*  --- Nutrients coming from biogas plant (including energy crops and purchased manure)

$iftheni.b %biogas% == true
         + v_nutBiogasDuev(nut,t,nCur)  *  p_nutEffectivDueVNvBiogas(nut)

$endif.b

*  --- Applied synthetic fertilizer

         + v_synthAppliedDueV(nut,t,nCur)


*  --- Nutrient from N fixation from legumes in grassland
         + sum(  (c_s_t_i(crops,plot,till,intens)) ,
                   v_cropHa(crops,plot,till,intens,t,nCur) *   p_NfromLegumes(Crops)  )       $ sameas (nut,"N")


* --- Import of manure
*     [TK][TO DO] add coefficient for accounting for imported manure

       $$iftheni.im "%AllowManureImport%" == "true"

         +   sum ( (nut2_nut(nut2,nut),m),   v_manImport(t,nCur,m) *    p_nut2inMan(nut2,"manImport","LiquidImport") )

       $$endif.im

*  --- Crop output (nutrient removal)

         -   v_NutRemovalDuev(nut,t,nCur)

$iftheni.h %herd% == true

*   --- Nutrients exported from farm

      $$iftheni.ExMan %AllowManureExport%==true

        -  sum( (curManChain,m,nut2) $(not sameas (nut2,"P")), v_nut2export(curManChain,nut2,t,nCur,m) )  $ sameas (nut,"N")
        -  sum( (curManChain,m), v_nut2export(curManChain,"P",t,nCur,m) )                                 $ sameas (nut,"P")

      $$endif.ExMan

      $$iftheni.emissionRight not "%emissionRight%"==0

        -  sum( (curManChain,m,nut2) $(not sameas (nut2,"P")), v_nut2exportMER(curManChain,nut2,t,nCur,m) )  $ sameas (nut,"N")
        -  sum( (curManChain,m),                               v_nut2exportMER(curManChain,"P",t,nCur,m) )   $ sameas (nut,"P")

      $$endif.emissionRight

$endif.h

         =e=

             v_surplusDueV(t,nCur,nut)      ;
```

The surplus, *v\_surplusDueV,* is not allowed to exceed a certain
threshold which is changing from Fertilization Ordinance 2007 to 2017 and, in addition, can be defined in the GUI.

[embedmd]:# (N:/em/work1/FarmDyn/FarmDyn_QM/gams/model/duev_module.gms GAMS /nutSurplusDueVRestr_.*?\.\./ /;/)
```GAMS
nutSurplusDueVRestr_ (tCur(t),nCur,nut)   $ (p_surPlusDueVMax(t,nut) $ t_n(t,nCur))  ..

       v_surplusDueV(t,nCur,nut)

         =L=
               p_surplusDueVMax(t,nut) *    v_croplandActive(t,nCur) *  ( 1 - p_soilShareNutEnriched)  $ sameas (nut,"P")

                  +     p_surplusDueVMax(t,nut) *    v_croplandActive(t,nCur)     $ sameas (nut,"N")

                  ;
```


## Organic nitrogen application threshold

Farms have to calculate the application of manure N and, under the Fertilization Ordinance 17, N from biogas digestate. The derived value
is not allowed to exceed a threshold related to farm area in ha. In most cases, this value is 170 kg N/ha/a which
is directly derived from the EU Nitrates Directive.

As input for manure N, the animal excretion is included *v_nutExcrDuev(nut,t,nCur)\_*, and the input from biogas in calculated
seperatly in the following equation.

@Rienne:  add equation model\duev_module.gms -   nutBiogasDueVAccAL_

The N input from manure and biogas digestate as well as manure import is summarized in the following equation. The export of manure
is substracted in the equation. The variable *v_DueVOrgN* returns the accourance of nitrogen from organic sources at a farm level.
The nitrogen has to be accounted with factors defined by the Fertilization Ordinance.

@Rienne:  add equation model\duev_module.gms - DuevOrgN_

The N input is not allowed to exceed a target value defined by the Fertilizations Ordinace, being 170 kg N/ha/a in most cases.

@Rienne:  add equation model\duev_module.gms -  DuevOrgNLimit_

## Binding fertilizer planning

Under the Fertilization Ordinance 2017, farms have to do an obligatory fertilizer planning based on the expected yields.
The derived nutrient need with regard to nitrogen must not be exceeded. This allows to calculate a nitrogen
quota which farms have to meet. The Fertilizer quota is always calculated, if the Fertilization Ordinance is switched on.
However, it only becomes binding for fertilizer application under the Fertilization Ordinance 2017.

The nutrient input is summarized in the equation *FertQuotaInput_*. Nutrients from chemical fertilizer, manure and mineralization
from the soil are taken into account. Manure N is accounted with mineral fertilizer equivalents defined by the Ordinace.

@Rienne:  add equation model\duev_module.gms - FertQuotaInput_

Furtermore, the plant nutrient need is calculated. It depends on the yield level and is precisley defined by the Fertilization
Ordinance.

@Rienne:  add equation model\duev_module.gms - FertQuotaNeed_

The plant nutrient input is not allowed to exceed the estimated nutrient need. The restriction becomes only binding under the Fertilization Ordiance
2017. p_bigNumberFO, being a very large number if the Fertilization Ordinance 2017 is not activated, ensures that the nutrient need is extremly high under the Fertilization Ordinance
2007 and not binding.

@Rienne:  add equation model\duev_module.gms -  FertQuota_

## Required manure storage capacities

Farms are required to hold a minimum storage capacity to bridge the time in autumn and winter when manure application is not allowed. This storage
capacity was defined at federal state level under the Fertilization Ordinance 2007. Under the Fertilization Ordinance 2017, it is defined at federal level.

Generally, farms have to hold a manure storage capacity to gap the amount of manure excretion corresponding to a certain time period, e.g. 6 months.
Therefore, the required storage capacity is defined in the manure module. The parameter *p_ManureStorageNeed* defines the required amount of months
and is linked to the selected Fertilization Ordinance or can be defined in the GUI.

@Rienne: add equation model\manure_module.gms - manStorCapNeed_

Under the Fertilization Ordinance 2017, farms exceeding a stocking density of 3 livestock units per ha have to hold additional manure storage capacity.
This is implemented with a binary trigger in Farmdyn. The variable *v_triggerStorageGVha* becomes one when the farm exceeds the livestock unit
threshold.

@Rienne:  add equation model\duev_module.gms - triggerStorageGVha_

If the variable *v_triggerStorageGVha* is one, the restriction in the following equation becomes binding.

@Rienne:  add equation model\duev_module.gms -  manStorCapGVDepend_

## Banning periods for fertilizer application

During certain months of the year, the application of fertilizer is not allowed as there is no plant nutrient need and the risk of nitrate leaching is very high.
This is implemented in Farmdyn by setting the variable *v_mandist* and *v_syntdist* to zero for certain months which disables manure application in the model.

Depending on the Fertilization Ordinance selected, sets are defined which include the months in which fertilizer application is forbidden (can also be defined
via the GUI which is not shown here).

@Rienne: I just coppied the code from coeffgen\fertilizing.gms, please us markdown to make a proper linkage

   $$elseifi.fertGui %RegulationFert% == FD_2007

*      --- (2) Depending on regulation of FO 07

       set  monthApplicationForbidden(m)                    /Dec,Jan /  ;
       set  monthApplicationForbiddenArab(m)            /Nov,Dec,Jan /  ;
       set  monthApplicationForbiddenGrass(m)              / Dec,Jan /  ;


   $$elseifi.fertGui %RegulationFert% == FD_2017

*      --- (3) Depending on regulation of FO 17
       set  monthApplicationForbidden(m)              /Nov,Dec,Jan / ;
       set  monthApplicationForbiddenArab(m)      /Oct,Nov,Dec,Jan / ;
       set  monthApplicationForbiddenGrass(m)        / Nov,Dec,Jan / ;

   $$endif.fertGui

For the months which are defined in the described sets, the varialbes for fertilizer application are set to zero.

@Rienne: I just coppied the code from coeffgen\fertilizing.gms, please us markdown to make a proper linkage


 v_syntDist.up(arabCrops(crops),plot,till,intens,syntFertilizer,t,n,monthApplicationForbiddenArab(m))
                                                      $ ( t_n(t,n) $ c_s_t_i(crops,plot,till,intens) )  = 0 ;

     v_syntDist.up(grassCrops(crops),plot,till,intens,syntFertilizer,t,n,monthApplicationForbiddenGrass(m))
                                                      $ ( t_n(t,n) $ c_s_t_i(grassCrops,plot,till,intens) )  = 0 ;

     v_syntDist.up(arabCrops(crops),plot,"eco",intens,syntFertilizer,t,n,m)
                                                      $ ( t_n(t,n) $ c_s_t_i(crops,plot,"eco",intens) )  = 0 ;


     v_syntDist.up(grassCrops(crops),plot,till,intens,syntFertilizer,t,n,m)
                                                      $ ( t_n(t,n) $ c_s_t_i(grassCrops,plot,till,intens)
                                                      $ sum(c_s_t_i(crops1,plot1,"eco",intens),1))  = 0 ;




   $$iftheni.v_manDist declared v_manDist

     v_volManApplied.up(manChain,t,n,monthApplicationForbidden) $ t_n(t,n)  = 0;
     v_nut2ManApplied.up(manChain,nut2,t,n,monthApplicationForbidden) $ t_n(t,n) = 0;
     v_manDist.up(crops,plot,till,intens,manApplicType,manType,t,n,monthApplicationForbidden)
        $ (t_n(t,n) $  c_s_t_i(crops,plot,till,intens)) = 0;

     v_manDist.up(arabCrops(crops),plot,till,intens,manApplicType,manType,t,n,monthApplicationForbiddenArab)
        $ (t_n(t,n) $ c_s_t_i(crops,plot,till,intens)) = 0;

     v_manDist.up(grassCrops(crops),plot,till,intens,manApplicType,manType,t,n,monthApplicationForbiddenGrass)
        $ (t_n(t,n) $ c_s_t_i(crops,plot,till,intens)) = 0;

   $$endif.v_manDist

## Restriction of fertilizer application in autumn

In addition to the fixed banning periods, the application of fertilizer in autumn is only legal for some crops and restricted
to a defined amount of nitrogen per ha. The parameter *p_NLimitInAutumn* contains the allowed amount of N and is
defined depending on the Fertilization Ordinance. Catch crops allow additional manure application in autumn.

@Rienne:  add equation model\duev_module.gms - NLimitAutumn_


## Low-emission manure application techniques

The Fertilization Ordinance defines which manure application techniques are legally allowed. Under the FO 17, broadcast spreading
is banned except on fallow land followed by direct incorporation. This measures is introduced in Farmdyn by setting the variable
v_mandist to zero for certain months and not allowed application techniques.

@Rienne: I just coppied the code from coeffgen\fertilizing.gms, please us markdown to make a proper linkage

* --- Broadcast spreader are banned on grassland

   $$iftheni.dh %cattle%==true
      v_manDist.up(grassCrops(crops),plot,till,intens,"applSpreadPig",manType,t,nCur,m) $ t_n(t,Ncur)   = 0   ;
      v_manDist.up(grassCrops(crops),plot,till,intens,"applSpreadCattle",manType,t,nCur,m) $ t_n(t,Ncur)   = 0   ;
   $$endif.dh

* --- Broadcast spreader are generally banned on arable land, except when there is no crop

   v_manDist.up(arabCrops(crops),plot,till,intens,"applSpreadPig",manType,t,nCur,m) $ (t_n(t,Ncur) $ monthGrowthCrops(crops,m))    = 0   ;
   v_manDist.up(arabCrops(crops),plot,till,intens,"applSpreadCattle",manType,t,nCur,m) $ (t_n(t,Ncur) $ monthGrowthCrops(crops,m))    = 0   ;


